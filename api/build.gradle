apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = "com.uwetrottmann.seriesguide"
version = "2.1.1-SNAPSHOT"

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion 26
    }

    compileOptions {
        encoding = "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        // for CI server (reports are not public)
        textReport true
        textOutput 'stdout'
    }
}

dependencies {
    implementation "androidx.core:core:${versions.core}"
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    title = "SeriesGuide API"

    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // Add in dependency classpath to avoid cannot find symbol (http://stackoverflow.com/a/34572606/1676363)
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }

    options.locale = "en"
    options.windowTitle = "SeriesGuide API"
    options.links("https://developer.android.com/reference/")
    options.linksOffline("https://developer.android.com/reference/", "https://developer.android.com/reference/androidx/")
    options.noDeprecated = true
    options.noHelp = true
    options.noIndex = true
    options.noTree = true
    options.stylesheetFile = file('./javadoc_stylesheet.css')
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate { project ->
    publishing {
        publications {
            central(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                artifact androidJavadocsJar
                artifact androidSourcesJar

                artifactId = 'seriesguide-api'

                pom {
                    name = "SeriesGuide API"
                    description = "Extension API for SeriesGuide to provide custom actions on media items"
                    url = "https://seriesgui.de/api"

                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }

                    developers {
                        developer {
                            id = "uwetrottmann"
                            name = "Uwe Trottmann"
                        }
                    }

                    scm {
                        connection = "scm:git:git@github.com:UweTrottmann/SeriesGuide.git"
                        developerConnection = "scm:git:git@github.com:UweTrottmann/SeriesGuide.git"
                        url = "https://github.com/UweTrottmann/SeriesGuide.git"
                    }
                }
            }
        }

        repositories {
            maven {
                name = "Central"
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                if (rootProject.hasProperty('SONATYPE_NEXUS_USERNAME')
                        && rootProject.hasProperty('SONATYPE_NEXUS_PASSWORD')) {
                    credentials {
                        username SONATYPE_NEXUS_USERNAME
                        password SONATYPE_NEXUS_PASSWORD
                    }
                }
            }
        }
    }

    signing {
        if (!rootProject.hasProperty('signing.keyId')
                || !rootProject.hasProperty('signing.password')
                || !rootProject.hasProperty('signing.secretKeyRingFile')) {
            println "WARNING: Signing properties missing, published artifacts will not be signed."
        }
        sign publishing.publications.central
    }

}
