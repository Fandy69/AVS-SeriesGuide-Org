// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = '1.2.71' // https://github.com/JetBrains/kotlin/blob/master/ChangeLog.md
    ext.versions = [
            'minSdk': 15,
            'compileSdk': 28,
            'targetSdk': 26,

            'supportLibrary': '28.0.0', // developer.android.com/topic/libraries/support-library/revisions.html
            'lifecycle': '1.1.1', // developer.android.com/topic/libraries/architecture/release-notes.html
            'playServices': '11.4.0', // developers.google.com/android/guides/releases
            'room': '1.1.1', // https://developer.android.com/jetpack/docs/release-notes

            'butterknife': '8.8.1', // github.com/JakeWharton/butterknife/blob/master/CHANGELOG.md
            'crashlytics': '2.9.5', // docs.fabric.io/android/changelog.html
            'dagger': '2.17', // github.com/google/dagger/releases
            'gson': '2.8.5', // github.com/google/gson/blob/master/CHANGELOG.md
            'okhttp': '3.11.0', // github.com/square/okhttp/blob/master/CHANGELOG.md
            'timber': '4.7.1', // github.com/JakeWharton/timber/blob/master/CHANGELOG.md

            'androidUtils': '2.3.1', // github.com/UweTrottmann/AndroidUtils/blob/master/RELEASE_NOTES.md
            'thetvdb': '1.6.1', // github.com/UweTrottmann/thetvdb-java/blob/master/CHANGELOG.md
            'tmdb': '1.10.1', // github.com/UweTrottmann/tmdb-java/blob/master/CHANGELOG.md
            'trakt': '5.10.0', // github.com/UweTrottmann/trakt-java/blob/master/CHANGELOG.md

            'truth': '0.42', // github.com/google/truth/releases
            'threetenABP': '1.1.0', // github.com/JakeWharton/ThreeTenABP/blob/master/CHANGELOG.md

            // version 15xxxyy -> min SDK 15, release xxx, build yy
            'code': 1503300,
            'name': '45-beta1',
    ]
    ext.isCiBuild = System.getenv('CI') == 'true'

    repositories {
        jcenter()
        google()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0' // libraries, SeriesGuide
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:2.0.0' // SeriesGuide
        classpath 'io.fabric.tools:gradle:1.26.0' // SeriesGuide
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.10.0' // api
    }
}

apply plugin: 'com.github.ben-manes.versions'

// reject preview releases for dependencyUpdates task
dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Preview release')
            }
        }
    }
}

allprojects {
    repositories {
        jcenter()
        google()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
}

task clean(type: Delete) {
    group "build"
    delete rootProject.buildDir
}

wrapper {
    //noinspection UnnecessaryQualifiedReference
    distributionType = Wrapper.DistributionType.ALL
}
